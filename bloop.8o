#----------------------------#
#  bloop
#  author: jackie kircher
#----------------------------#

#-- sprites --#
: podoop #11x12
  0x0F 0x00 0x1F 0x80 0x1F 0x80 0x1F 0x80
  0x1F 0x80 0x0A 0x80 0x0F 0x00 0x1A 0x80
  0x2A 0x80 0x2A 0x80 0xAA 0xA0 0x51 0x40
  0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00

: podoop-head #6x7
  0x78 0xFC 0xFC 0xFC 0xFC 0x54 0x78

: tentacle-tip
  0x80

: tentacle
  0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00
  0x00 0x00 0x00 0x00 0x00 0x00 0x00

#-- setup --#

  # registers
  :alias podoop_x   va
  :alias podoop_y   vb
  :alias tentacle_x vc
  :alias tentacle_y vd


#-- drawing --#

# draws the tentacle
: draw-tentacle
  i := tentacle-tip
  sprite tentacle_x tentacle_y 1
;


: main
  hires

  podoop_x := 60
  podoop_y := 24
  i        := podoop-head
  sprite podoop_x podoop_y 7

  tentacle_x := podoop_x
  tentacle_y := podoop_y
  tentacle_y += 7
  i          := tentacle-tip
  draw-tentacle

  loop
    move-tentacle
  again

;

: move-tentacle
  :alias input  v0
  :alias move_x v1
  :alias move_y v2

  move_x := 0
  move_y := 0

  input := 5 #w
  if input key then move_y += -1
  input := 7 #a
  if input key then move_x += -1
  input := 8 #s
  if input key then move_y +=  1
  input := 9 #d
  if input key then move_x +=  1

  # return early and don't change anything if no
  # direction keys are being pressed
  v0 := 0
  if move_x != 0 then v0 := 1
  if move_y != 0 then v0 := 1
  if v0 == 0 then return

  move_x += tentacle_x
  move_y += tentacle_y

  # DEBUG #
    # erase old tentacle sprite
    i  := tentacle
    v0 := 0
    sprite v0 v0 15


  # anchor point
  v7 := podoop_x
  v8 := podoop_y
  v8 += 7

  # x coordinate handling
  v3 := move_x
  v3 -= v7
  v4 := 1
  if vf == 0 begin
    v4 := -1
    v3 := v7
    v3 -= move_x
  end

  if v4 ==  1 then v6 := 0x80
  if v4 == -1 then v6 := 0x01
  if v3 ==  0 then v6 := 0x01
  loop
    while v3 != 0
      if v4 ==  1 then v6 >>= v6 #tip is moving +x
      if v4 == -1 then v6 <<= v6 #tip is moving -x
      v3 +=  -1
  again

  # y coordinate handling
  v9 := 7  #default row is in the middle of the sprite
  v3 := move_y
  v3 -= v8
  v4 := 1
  if vf == 0 begin
    v4 := -1
    v3 := v8
    v3 -= move_y
  end

  loop #adjust the row by setting the srpite instruction offset
    while v3 != 0
      v9 += v4
      v3 += -1
  again


  i  := tentacle
  i  += v9
  load v0

  v3 := v0
  v3 ^= v6
  
  # 01000000 v6
  # 01111100 v0
  # 00111100 v0 ^= v6

  # 01000000 v6
  # 11000011 NOT v0
  # 01000000 v6 &= v0 AND
  v5 := v3
  v4 := 0xFF
  v5 ^= v4 # bitwise NOT of v0
  v7 := v6
  v7 &= v5
  if v7 == v6 begin # backtracking

    # erase where we were and do not erase where we are
    move_x -= tentacle_x
    move_y -= tentacle_y

    if move_x ==  1 then v6 <<= v6
    if move_x == -1 then v6 >>= v6
    if move_y ==  1 begin
      v9 += -1
      i  := tentacle
      i  += v9
      load v0
    end
    if move_y == -1 begin
      v9 += 1
      i  := tentacle
      i  += v9
      load v0
    end

    v3 := v0
    v3 ^= v6

    draw-tentacle
    tentacle_x += move_x
    tentacle_y += move_y
  else
    tentacle_x := move_x
    tentacle_y := move_y
    draw-tentacle
  end


  v0 := v3
  i  := tentacle
  i  += v9
  save v0


  # DEBUG #
    # draw new tentacle sprite
    i  := tentacle
    v0 := 0
    sprite v0 v0 15
;
